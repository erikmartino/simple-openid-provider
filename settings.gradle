pluginManagement {
	resolutionStrategy {
		eachPlugin {
			if (requested.id.id == 'org.springframework.boot') {
				useModule 'org.springframework.boot:spring-boot-gradle-plugin:' + requested.version
			}
		}
	}

	repositories {
		gradlePluginPortal()
		maven { url 'https://repo.spring.io/libs-milestone/' }
	}
}

rootProject.name = 'simple-openid-provider'

enableFeaturePreview('STABLE_PUBLISHING')

FileTree buildFiles = fileTree(rootDir) {
	include '**/*.gradle'
	exclude '/gradle', 'build.gradle', 'settings.gradle'
}

buildFiles.each { File buildFile ->
	String projectName = buildFile.name.replace('.gradle', '')
	String projectPath = ':' + projectName
	include projectPath
	def project = findProject(projectPath)
	project.name = projectName
	project.projectDir = buildFile.parentFile
	project.buildFileName = buildFile.name
}
